// Export for https://community.bistudio.com/wiki/Spearhead_1944_CfgVehicles_Backpacks

_toExport = "backpack";

private _text = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;
_text = _text + "{| class=""wikitable sortable"" border=""1"" style=""border-collapse:collapse; font-size:80%;"" cellpadding=""3px""" + endl;

_text = _text + format ["! %1 !! %2 !! %3 !! %4 !! %5 !! %6 !! %7", "Preview", "Class", "Name", "Inventory description", "Weight", "Capacity", "Used by"] +  endl + "|-" + endl;

private _classes = "(configSourceMod _x == ""SPE"")" configClasses (configfile >> "CfgVehicles");

{
	if ((getNumber (_x >> "scope") < 2) || (((configName _x) call BIS_fnc_itemType select 1) != _toExport)) then {continue};

	private _picture = getText (_x >> "picture");
	private _class = configName _x;
	private _displayName = getText (_x >> "displayName");
	private _descriptionShort = getText (_x >> "descriptionShort");
	private _libraryText = getText (_x >> "Library" >> "libTextDesc");
	private _mass = getNumber (_x >> "mass");
	private _capacity = getNumber (_x >> "maximumLoad");

	private _magazines = compatibleMagazines _class;
	private _accessories = compatibleItems _class;

	_picture = _picture splitString "\";
	reverse _picture;
	_picture = _picture param [0, ""];
	_picture = _picture splitString ".";
	_picture = _picture param [0, ""];

	if (_picture != "") then
	{
		_picture = _picture + ".png";
	};

	_text = _text + "| " + (if (_picture != "") then {format ["[[File:%1|150px]]", _picture]} else {""}) + endl;
	_text = _text + "| " + (if (_class != "") then {_class} else {""}) + endl;
	_text = _text + "| " + (if (_displayName != "") then {format ["''%1''", _displayName]} else {""}) + endl;

	if (_libraryText != "") then
	{
		_descriptionShort = _descriptionShort + endl + endl + _libraryText;
	};

	_text = _text + "| " + (if (_descriptionShort != "") then {format ["''%1''", _descriptionShort]} else {""}) + endl;
	_text = _text + "| " + str _mass + endl;
	_text = _text + "| " + str _capacity + endl;

	private _usedBy = [];
	{
		if (_class == getText (_x >> "backpack")) then
		{
			_usedBy pushBack configName _x;
		};
	} forEach ("(configSourceMod _x == ""SPE"")" configClasses (configfile >> "CfgVehicles"));

	_text = _text + "|" + endl;

	if (_usedBy isNotEqualTo []) then
	{
		private _collapse = count _usedBy > (count _magazines max 10);
		if (_collapse) then
		{
			_text = _text + "{| class=""wikitable mw-collapsible mw-collapsed""" + endl;
			_text = _text + "! Objects" + endl;
			_text = _text + "|-" + endl;
			_text = _text + "|" + endl;
		};
		{
			_text = _text + "* " + _x + endl;
		} forEach _usedBy;

		if (_collapse) then
		{
			_text = _text + "|}" + endl;
		};
	};

	_text = _text + "|-" + endl;

} forEach _classes;

_text = _text + "|}" + endl + endl + "[[Category: Spearhead 1944]]";

copyToClipboard _text;