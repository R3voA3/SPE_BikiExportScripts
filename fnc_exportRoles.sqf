// For https://community.bistudio.com/wiki/Spearhead_1944_CfgRoles

private _condition = "(configSourceMod _x == ""SPE"")";
private _roles = _condition configClasses (configFile >> "CfgRoles");

private _export = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

_export = _export + "[[Spearhead 1944]] introduces new icons via a new property called ''texture'' and roles to CfgRoles. These icons are for example use for the custom Nametag system." + endl + endl;

_export = _export + "{| class=""wikitable sortable""" + endl + "|-" + endl;
_export = _export + "! style= ""width:150"" | Preview !! style= ""width:350px"" | Class Name !! style= ""width:250px"" | Display Name" + endl + "|-" + endl;

{
	private _name = getText (_x >> "displayname");
	private _classname = configName _x;
	private _texture = getText (_x >> "texture");

	if (_texture == "") then {continue};

	_texture = _texture splitString "\";
	reverse _texture;
	_texture = _texture param [0, ""];
	_texture = _texture splitString "." param [0, ""];

	if (_texture != "") then
	{
		_texture = format ["[[File:%1.png|150px|center]]", toLower _texture];
	};

	_export = _export + format ["| style=""background-color: #4c4c4c;"" |%1 || %2 || ''%3''", toLower _texture, _classname, _name] + endl + "|-" + endl;
} forEach _roles;

_export = _export + "|}" + endl + endl;

// Category
_export = _export + "[[Category: Spearhead 1944]]";

copyToClipboard _export;