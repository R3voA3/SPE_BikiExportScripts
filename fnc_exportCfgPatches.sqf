// For https://community.bistudio.com/wiki/Spearhead_1944_CfgPatches

params
[
	["_cfg", "CfgVehicles"],
	["_patches", "'WW2_SPE_' in configName _x" configClasses (configFile >> "CfgPatches") apply {configName _x}]
];

_patches = +_patches;

{
	_patches set [_foreachindex, tolower _x];
} foreach _patches;

_allPatches = _patches isEqualTo [];

_product = productversion select 0;
_productShort = productversion select 1;

_fnc_getItemPage = {
	switch (_this) do {
		case "Weapon": {"CfgWeapons Weapons"};
		case "VehicleWeapon": {"CfgWeapons Vehicle Weapons"};
		case "Item": {"CfgWeapons Items"};
		case "Equipment": {"CfgWeapons Equipment"};
		default {"CfgWeapons"};
	};
};

_text = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

_text = _text + "{| class=""wikitable sortable"" border=""1"" style=""border-collapse:collapse; font-size:100%;"" cellpadding=""3px""" + endl;
_text = _text + "! Class<br />" + endl;
_text = _text + "! Objects<br />" + endl;
_text = _text + "! Objects Gallery<br />" + endl;

{
	_class = configname _x;
	if (_allPatches || (tolower _class) in _patches) then {
		_textAddons = "";
		_textUnits = "";
		_textWeapons = "";
		_textSide = "";
		_galleryUnits = "";
		_galleryWeapons = "";

		_units = getarray (_x >> "units");
		if (_cfg == "cfgVehicles" && count _units == 0) exitwith {};
		_weapons = getarray (_x >> "weapons");
		if (_cfg != "cfgVehicles" && count _weapons == 0) exitwith {};


		{
			_textSide = if (_x iskindof "allvehicles") then {
				str ((getnumber (configfile >> "cfgvehicles" >> _x >> "side") min 3) call bis_fnc_sidetype)
			} else {
				"EMPTY"
			};

			_preview = getText (configFile >> "CfgVehicles" >> _x >> "editorPreview") splitString "\";
			reverse _preview;
			_preview = _preview param [0, ""];

			_textUnits = _textUnits + endl + format ["* %3", _product, _productShort, _x, _textSide];

			if (_preview != "") then
			{
				_galleryUnits = _galleryUnits + format ["[[File:%1|150px|&nbsp;]]", _preview];
			};
		} foreach _units;

		{
			if (isclass (configfile >> "cfgweapons" >> _x)) then {

				_preview = getText (configFile >> "CfgWeapons" >> _x >> "editorPreview") splitString "\";
				reverse _preview;
				_preview = _preview param [0, ""];

				_type = _x call bis_fnc_itemType;
				_page = (_type select 0) call _fnc_getItemPage;
				_textWeapons = _textWeapons + endl + format [":[[%1 %4#%3|%3]]", _product, _productShort, _x, _page];

				if (_preview != "") then
				{
					_galleryWeapons = _galleryWeapons + format ["[[File:%1|150px|&nbsp;]]", _preview];
				};
			};
		} foreach _weapons;


		_text = _text + "|-" + endl;
		_text = _text + "| " + format ["<span id=""%1"" >'''%1'''</span>", _class] + endl;
		if (_cfg == "CfgVehicles") then {
			_text = _text + "| " + "<small>" + _textUnits + endl + "</small>" + endl;
			_text = _text + "| " + "<small>" + _galleryUnits + endl + "</small>" + endl;
		} else {
			_text = _text + "| " + "<small>" + _textWeapons + endl + "</small>" + endl;
			_text = _text + "| " + "<small>" + _galleryWeapons + endl + "</small>" + endl;
		};
	};
} foreach ((configfile >> "cfgpatches") call bis_fnc_returnchildren);

_text = _text + "|}" + endl + endl;

_text = _text + format ["[[Category: Spearhead 1944]]", _product] + endl;

copytoclipboard _text;