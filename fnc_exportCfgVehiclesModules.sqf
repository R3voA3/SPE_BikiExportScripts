// For https://community.bistudio.com/wiki/Spearhead_1944_CfgVehicles_Modules

private _condition = "'SPE_' in configname _x && toLower (getText (_x >> ""vehicleClass"")) == ""modules"" && getNumber (_x >> ""scope"") == 2";
private _modules = _condition configClasses (configFile >> "CfgVehicles");
private _export = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

// Custom TOC
_export = _export + "{{TOC|normal|1|2}}" + endl + endl;

{
	private _displayName = getText (_x >> "displayName");
	private _category = getText (_x >> "Category");
	private _availableForZeus = str (getNumber (_x >> "scopeCurator") > 1);
	private _moduleDescription = getText (_x >> "ModuleDescription" >> "description");

	_category = getText (configFile >> "CfgFactionClasses" >> _category >> "displayName");

	_export = _export + format ["= %1 =", _displayName] + endl + endl;
	_export = _export + "'''Category:''' " + _category + endl + endl;
	_export = _export + "'''Available in Zeus:''' " + _availableForZeus + endl + endl;
	_export = _export + format ["'''Module Description:''' %1", [_moduleDescription, "N/A"] select (_moduleDescription == "")] + endl;

	// Get attributes
	_export = _export + "== Attributes ==" + endl;

	private _attributes = "true" configClasses (_x >> "Arguments");

	if (_attributes isEqualTo []) then
	{
		_attributes = "true" configClasses (_x >> "Attributes");
	};

	{
		private _displayName = getText (_x >> "displayName");
		private _property = getText (_x >> "property");

		// Skipt invisible attributes
		if (_displayName == "") then {continue};

		private _tooltip = getText (_x >> "tooltip");

		if (_tooltip == "") then
		{
			_tooltip = getText (_x >> "description");
		};

		_export = _export + format ["==== %1 ====", _displayName] + endl + endl;
		_export = _export + format ["'''Description:''' %1", [_tooltip, "N/A"] select (_tooltip == "")] + endl + endl;
		_export = _export + format ["'''Property:''' %1", [_property, "N/A"] select (_property == "")] + endl + endl;

		// Get available values for each attribute (combo control)
		if (isClass (_x >> "values")) then
		{
			_export = _export + "==== Values ====" + endl + endl;

			{
				private _name = getText (_x >> "name");
				_export = _export + "* " + _name + endl + endl;
			} forEach ("true" configClasses (_x >> "values"));
		};
	} forEach _attributes;

} forEach _modules;

// Category
_export = _export + "[[Category: Spearhead 1944]]";

copyToClipboard _export;