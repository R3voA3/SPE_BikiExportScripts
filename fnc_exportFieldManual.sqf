// For https://community.bistudio.com/wiki/Spearhead_1944_Field_Manual

forceUnicode 0;

private _FMCategories = "configSourceMod _x == ""SPE""" configClasses (configFile >> "CfgHints");

_FMCategories = [_FMCategories, [], {getNumber (_x >> "logicalOrder")}, "ASCEND"] call BIS_fnc_sortBy;

private _text = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

{
	private _cfgCategory = _x;
	private _configNameCategory = configName _cfgCategory;

	// Skip messages
	if (_configNameCategory == "DlcMessage") then {continue};

	private _displayName = getText (_cfgCategory >> "displayName");

	// Entries that have no displayName are not supposed to be in FM
	if (_displayName == "") then {continue};

	_text = _text + format ["= %1 = ", _displayName] + endl;

	// Special handling for IFS to allow transclusion
	if (_displayName == "Indirect Fire Support System") then
	{
		_text = _text + "<onlyinclude>" + endl;
	};

	{
		_cfgHint = _x;
		private _displayName = getText (_cfgHint >> "displayName");
		private _displayNameShort = getText (_cfgHint >> "displayNameShort");
		private _description = getText (_cfgHint >> "description");
		private _arguments = getArray (_cfgHint >> "arguments");
		private _tip = getText (_cfgHint >> "tip");
		private _vehicle = getText (_cfgHint >> "vehicle");
		private _weapon = getText (_cfgHint >> "weapon");
		private _preview = "";

		// Arguments need to be formatted
		private _argumentsFormatted = _arguments apply
		{
			switch true do
			{
				case (_x isEqualType ""):
				{
					if (isLocalized _x) then
					{
						localize _x;
					};
				};
				case (_x isEqualTypeParams [[""]]):
				{
					format ["{{hl|%1}}", actionKeysNames (_x param [0, []] param [0, ""])];
				};
				case (_x isEqualTypeParams [""]):
				{
					//format ["%1", if (isLocalized (_x#0)) then {format ["{{hl|%1}}", localize (_x#0)]} else {format ["{{hl|%1}}", _x#0]}];
					if (isLocalized (_x#0)) then {localize (_x#0)} else {_x#0}
				};
				default
				{
					"TODO";
				};
			};
		};

		if (_displayNameShort != "") then
		{
			_text = _text + format ["=== %1 ===", _displayNameShort] + endl;
		}
		else
		{
			_text = _text + format ["=== %1 ===", _displayName] + endl;
		};

		if (_vehicle != "") then
		{
			_preview = getText (configFile >> "CfgVehicles" >> _vehicle >> "editorPreview");

			if (_preview != "") then
			{
				_preview = _preview splitString "\";
				reverse _preview;
				_preview = format ["[[File: %1|Left]]%2%3", _preview param [0, ""], endl, "{{Clear|left}}"];
			};
		};

		if (_weapon != "") then
		{
			_preview = getText (configFile >> "CfgWeapons" >> _weapon >> "picture");

			if (_preview != "") then
			{
				_preview = _preview splitString "\";
				reverse _preview;

				_preview = format ["[[File: %1|Left]]%2%3", (_preview param [0, ""]) regexReplace [".paa", ".png"], endl, "{{Clear|left}}"];
			};
		};

		diag_log _preview;

		_text = _text + _preview + _description + endl;

		if (_tip != "") then
		{
			_text = _text + format ["{{Feature|informative|%1}}", _tip] + endl;
		};

		// Hardcoded placeholders. See https://community.bistudio.com/wiki/Arma_3:_Advanced_Hints_(Field_Manual)
		{
			_text = _text regexReplace ["%" + str (_forEachIndex + 11), _x];
		} forEach _argumentsFormatted;

		_text = _text regexReplace ["%1", endl];
		_text = _text regexReplace ["%2", endl + "*"];
		_text = _text regexReplace ["%3", "{{hl|"];
		_text = _text regexReplace ["%4", "}}"];

		// Special handling of Structured Text vs WikiText
		_text = _text regexReplace ["<a href='https://community.bistudio.com/wiki/Spearhead_1944'>BI wiki</a>", "[[Spearhead 1944]]"];

		// Extract file name from structured text and turn it into wiki code
		_text = _text regexReplace ["<img.*image='WW2.*\\(.*).paa'.\/>", "[[File: \L$1.png|256px|left]]{{Clear|left}}"];

		// Very Special handling
		_text = _text regexReplace ["<a href ='https://community.bistudio.com/wiki/Arma_3:_Enhanced_Multiplayer_Menu'>BI wiki</a>", "[[Arma 3: Enhanced Multiplayer Menu]]"];
		_text = _text regexReplace ["</t>", ""];
		_text = _text regexReplace ["<t %5>", ""];

	} forEach ("configSourceMod _x == ""SPE""" configClasses _cfgCategory);

	// Special handling for IFS to allow transclusion
	if (_displayName == "Indirect Fire Support System") then
	{
		_text = _text + "</onlyinclude>" +  endl;
	};
} forEach _FMCategories;

_text = _text + endl + "[[Category: Spearhead 1944]]";

copyToClipboard _text;