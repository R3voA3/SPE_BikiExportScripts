// For https://community.bistudio.com/wiki/Spearhead_1944_CfgMarkerColors

toFixed 2;

private _condition = "(configSourceMod _x == ""SPE"") && getNumber (_x >> ""scope"") > 0";
private _markers = _condition configClasses (configFile >> "CfgMarkerColors");

private _export = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

_export = _export + "{| class=""wikitable sortable""" + endl + "|-" + endl;
_export = _export + "! style= ""width:50px"" | Preview !! Class Name !! Display Name !! RGBA (0-1) !! RGB (0-255) !! HTML" + endl + "|-" + endl;

{
	private _classname = configName _x;
	private _name = getText (_x >> "name");
	private _colorRGBArma = getArray (_x >> "color");

	_colorRGBArma = _colorRGBArma apply
	{
		if (_x isEqualType "") then
		{
			call compile _x;
		}
		else
		{
			_x
		};
	};

	private _colorRGB255 = _colorRGBArma apply {_x * 255};

	_export = _export + format ["| style=""background-color: rgb(%1, %2, %3);"" | || %4 || ''%5'' || [%6, %7, %8, %9] || [%10, %11, %12] || %13", _colorRGB255#0, _colorRGB255#1, _colorRGB255#2, _className, _name, _colorRGBArma#0, _colorRGBArma#1, _colorRGBArma#2, _colorRGBArma#3,_colorRGB255#0, _colorRGB255#1, _colorRGB255#2, _colorRGBArma call BIS_fnc_colorRGBtoHTML] + endl + "|-" + endl;
} forEach _markers;

_export = _export + "|}" + endl + endl;

// Category
_export = _export + "[[Category: Spearhead 1944]]";

copyToClipboard _export;