// For https://community.bistudio.com/wiki/Spearhead_1944_Sounds#CfgMusic

private _condition = "(configSourceMod _x == ""SPE"")";
private _cfgs = ["CfgMusic", "CfgSounds"];

private _export = "{{TOC}}" + endl + endl;
_export = _export +  "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

{
	private _cfg = _x;

	// Headline
	_export = _export + format ["= %1 =", _cfg] + endl;

	// Table header
	_export = _export + "{| class=""wikitable sortable""" + endl + "|-" + endl;

	switch (_cfg) do
	{
		case "CfgSounds":
		{
			_export = _export + "! Class Name !! Display Name !! Duration (MM:SS.MS) !! File" + endl + "|-" + endl;

			{
				private _classname = configName _x;
				private _name = getText (_x >> "name");
				private _duration = getNumber (_x >> "duration");
				private _file = getArray (_x >> "sound") param [0, "N/A"];

				_export = _export + format ["| %1 || %2 || %3 || %4", _classname, if (_name != "") then {format ["''%1''", _name]} else {""}, [_duration, "MM:SS.MS"] call BIS_fnc_secondsToString, _file] + endl + "|-" + endl;
			} forEach (_condition configClasses (configFile >> _cfg));
		};
		case "CfgMusic":
		{
			_export = _export + "! Class Name !! Display Name  !! Music class !! Duration (MM:SS.MS) !! File" + endl + "|-" + endl;

			{
				private _classname = configName _x;
				private _name = getText (_x >> "name");
				private _musicClass = getText (_x >> "musicClass");
				private _duration = getNumber (_x >> "duration");
				private _file = getArray (_x >> "sound") param [0, "N/A"];

				_export = _export + format ["| %1 || %2 || %3 || %4 || %5", _classname, if (_name != "") then {format ["''%1''", _name]} else {""}, _musicClass, [_duration, "MM:SS.MS"] call BIS_fnc_secondsToString, _file] + endl + "|-" + endl;
			} forEach (_condition configClasses (configFile >> _cfg));
		};
	};
	_export = _export + "|}" + endl + endl;
} forEach _cfgs;

// Category
_export = _export + "[[Category: Spearhead 1944]]";

copyToClipboard _export;