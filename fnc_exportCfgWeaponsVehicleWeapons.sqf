// For https://community.bistudio.com/wiki/Spearhead_1944_CfgWeapons_Vehicle_Weapons

private _text = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;
_text = _text + "{| class=""wikitable sortable""" + endl;

_text = _text + format ["! %1 !! %2 !! %3 !! %4 !! %5", "Class Name", "Display Name", "Magazines", "Used by", "Used by (gallery)"] +  endl + "|-" + endl;

private _vehicleWeapons = "(configSourceMod _x == ""SPE"" && (getNumber (_x >> ""scope"") > 0)) && (((configName _x) call BIS_fnc_itemType select 0) == ""VehicleWeapon"")" configClasses (configfile >> "cfgweapons");

{
	private _class = configName _x;
	private _displayName = getText (_x >> "displayName");

	private _magazines = compatibleMagazines _class;

	_text = _text + "| " + (if (_class != "") then {_class} else {""}) + endl;
	_text = _text + "| " + (if (_displayName != "") then {format ["''%1''", _displayName]} else {""}) + endl;

  _text = _text + "|" + endl;

  if (_magazines isNotEqualTo []) then
  {
    {
      if (getNumber (configFile >> "CfgMagazines" >> _x >> "scope") != 2) then {continue};
      _text = _text + "* " + _x + endl;
    } forEach _magazines;
  };

  private _usedBy = [];
  {
    private _classVehicle = configName _x;
    {
      private _turretWeapons = getArray (_x >> "weapons");
      if (_class in _turretWeapons) then
      {
        _usedBy pushBack _classVehicle;
      };
    } forEach ([configName _x] call BIS_fnc_getTurrets);
  } forEach ("(configSourceMod _x == ""SPE"") && getNumber (_x >> ""scope"") == 2" configClasses (configfile >> "CfgVehicles"));

  _text = _text + "|" + endl;
  private _galleryImagesText = "";

  if (_usedBy isNotEqualTo []) then
	{
    private _collapse = count _usedBy > (count _magazines max 10);
    if (_collapse) then
    {
      _text = _text + "{| class=""wikitable mw-collapsible mw-collapsed""" + endl;
      _text = _text + "! Objects" + endl;
      _text = _text + "|-" + endl;
      _text = _text + "|" + endl;
    };
		{
			_text = _text + "* " + _x + endl;

      // Gallery
      private _preview = getText (configFile >> "CfgVehicles" >> _x >> "editorPreview");
      _preview = _preview splitString "\";
      reverse _preview;
      _preview = _preview param [0, ""];

      if (_preview != "") then
      {
        _galleryImagesText = _galleryImagesText + format ["[[File: %1|250px|center]] ", _preview];
      };
		} forEach _usedBy;

    if (_collapse) then
    {
      _text = _text + "|}" + endl;
    };
	};

  _text = _text + "|" + endl;
  _text = _text + _galleryImagesText;
	_text = _text + endl + "|-" + endl;
} forEach _vehicleWeapons;

_text = _text + "|}" + endl + endl + "[[Category: Spearhead 1944]]";

copyToClipboard _text;