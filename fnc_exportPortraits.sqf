// For https://community.bistudio.com/wiki/Spearhead_1944_Portraits

private _portraits = [];

(addonFiles ["WW2\SPE_Missions_p\UtilityFunctions_f\", ".paa"]) apply
{
	if ("_face" in _x) then
	{
		_portraits pushBack _x;
	};
};

_portraits sort true;

private _export = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;

_export = _export + "{| class=""wikitable""" + endl + "|-" + endl;
_export = _export + "! Path !! Preview" + endl + "|-" + endl;

_portraits apply
{
	private _imageName = _x splitString "\";
	_imageName = toLower (_imageName select (count _imageName -1));
	_imageName = _imageName regexReplace [".paa", ".png"];
	_export = _export + format ["| %1 || [[File: %2]]%3|-%3", _x, _imageName , endl];
};

_export = _export + "|}" + endl + endl;

// Category
_export = _export + "[[Category: Spearhead 1944]]";

copyToClipboard _export;