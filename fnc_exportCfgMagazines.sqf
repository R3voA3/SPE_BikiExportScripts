// Export for https://community.bistudio.com/wiki/Spearhead_1944_CfgMagazines

private _text = "{{Feature|important|The content of this page was generated by script. Manual edits might get lost.}}" + endl;
_text = _text + "{| class=""wikitable sortable"" border=""1"" style=""border-collapse:collapse; font-size:80%;"" cellpadding=""3px""" + endl;

_text = _text + format ["! %1 !! %2 !! %3 !! %4 !! %5 !! %6", "Preview", "Class Name", "Display Name", "Inventory Description", "Magazine Wells", "Used by"] +  endl + "|-" + endl;

private _cfgMagazines = "(configSourceMod _x == ""SPE"") && getNumber (_x >> ""scope"") == 2" configClasses (configfile >> "CfgMagazines");
private _cfgMagazineWells = "(configSourceMod _x == ""SPE"")" configClasses (configfile >> "CfgMagazineWells");
private _cfgWeapons = "(configSourceMod _x == ""SPE"") && getNumber (_x >> ""scope"") == 2" configClasses (configfile >> "CfgWeapons");

{
	if ((getNumber (_x >> "scope") < 2)) then {continue};

	private _picture = getText (_x >> "picture");
	private _class = configName _x;
	private _displayName = getText (_x >> "displayName");
	private _descriptionShort = getText (_x >> "descriptionShort");
	private _libraryText = getText (_x >> "Library" >> "libTextDesc");

	private _magazines = compatibleMagazines _class;
	private _accessories = compatibleItems _class;

  _picture = _picture splitString "\";
  reverse _picture;
  _picture = _picture param [0, ""];
  _picture = _picture splitString ".";
  _picture = _picture param [0, ""]; // Make them lower case to prevent issues with casing when uploading to BIKI

  if (_picture != "") then
  {
    _picture = _picture + ".png";
  };

	_text = _text + "| " + (if (_picture != "") then {format ["[[File:%1|150px]]", _picture]} else {""}) + endl;
	_text = _text + "| " + (if (_class != "") then {_class} else {""}) + endl;
	_text = _text + "| " + (if (_displayName != "") then {format ["''%1''", _displayName]} else {""}) + endl;

	if (_libraryText != "") then
	{
		_descriptionShort = _descriptionShort + endl + endl + _libraryText;
	};

	_text = _text + "| " + (if (_descriptionShort != "") then {format ["''%1''", _descriptionShort]} else {""}) + endl;

  private _magazineWells = [];

  {
    private _inWell = _class in (getArray (_x >> "SPE_Magazines"));

    if (_inWell) then
    {
      _magazineWells pushBack configName _x;
    };
  } forEach _cfgMagazineWells;

  _text = _text + "|" + endl;

  if (_magazineWells isNotEqualTo []) then
	{
    private _collapse = count _magazineWells > 10;
    if (_collapse) then
    {
      _text = _text + "{| class=""wikitable mw-collapsible mw-collapsed""" + endl;
      _text = _text + "! Objects" + endl;
      _text = _text + "|-" + endl;
      _text = _text + "|" + endl;
    };
		{
			_text = _text + "* " + _x + endl;
		} forEach _magazineWells;

    if (_collapse) then
    {
      _text = _text + "|}" + endl;
    };
	};

  _text = _text + "|" + endl;

  private _usedBy = [];
  {
    private _magazines = compatibleMagazines configName _x;

    if (_class in _magazines) then
    {
      _usedBy pushBack configName _x;
    };
  } forEach _cfgWeapons;

  if (_usedBy isNotEqualTo []) then
	{
    private _collapse = count _usedBy > (count _magazines max 10);
    if (_collapse) then
    {
      _text = _text + "{| class=""wikitable mw-collapsible mw-collapsed""" + endl;
      _text = _text + "! Objects" + endl;
      _text = _text + "|-" + endl;
      _text = _text + "|" + endl;
    };
		{
			_text = _text + "* " + _x + endl;
		} forEach _usedBy;

    if (_collapse) then
    {
      _text = _text + "|}" + endl;
    };
	};

	_text = _text + "|-" + endl;

} forEach _cfgMagazines;

_text = _text + "|}" + endl + endl + "[[Category: Spearhead 1944]]";

copyToClipboard _text;